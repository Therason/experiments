uniform vec2 uMouse;
uniform float uAspect;
uniform float uTime;
uniform vec3 uColor;

varying vec2 vUv;

// Simplex 2D noise
//
vec3 permute(vec3 x) { return mod(((x*34.0)+1.0)*x, 289.0); }

float snoise(vec2 v){
  const vec4 C = vec4(0.211324865405187, 0.366025403784439,
           -0.577350269189626, 0.024390243902439);
  vec2 i  = floor(v + dot(v, C.yy) );
  vec2 x0 = v -   i + dot(i, C.xx);
  vec2 i1;
  i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
  vec4 x12 = x0.xyxy + C.xxzz;
  x12.xy -= i1;
  i = mod(i, 289.0);
  vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
  + i.x + vec3(0.0, i1.x, 1.0 ));
  vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy),
    dot(x12.zw,x12.zw)), 0.0);
  m = m*m ;
  m = m*m ;
  vec3 x = 2.0 * fract(p * C.www) - 1.0;
  vec3 h = abs(x) - 0.5;
  vec3 ox = floor(x + 0.5);
  vec3 a0 = x - ox;
  m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );
  vec3 g;
  g.x  = a0.x  * x0.x  + h.x  * x0.y;
  g.yz = a0.yz * x12.xz + h.yz * x12.yw;
  return 130.0 * dot(m, g);
}

float smin(float a, float b, float k) {
    float h = clamp(0.5 + 0.5 * (b - a) / k, 0.0, 1.0);
    return mix(b, a, h) - k * h * (1.0 - h);
}

void main() {
  vec2 uv = vUv;
  uv.x *= uAspect;
  vec2 secondBall = vec2(0.75, 0.75);
  float secondDistance = distance(secondBall * vec2(uAspect, 1.0), uv);
  secondDistance += snoise(2.0 * (uv + uTime * 0.07)) * 0.05;

  float mouseDistance = distance(uMouse * vec2(uAspect, 1.0), uv);
  float noise = snoise(((uv * 0.05) + uTime * 0.005) * 30.) * 0.09;
  mouseDistance += noise;
  mouseDistance = smin(mouseDistance, secondDistance + 0.15, 0.1);
  float strength = step(0.75, 1.0 - mouseDistance);

  gl_FragColor = vec4(uColor, strength);
}